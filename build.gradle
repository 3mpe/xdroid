buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.0'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.10.1'
    }
}

subprojects {
    version VERSION_NAME

    apply plugin: 'android-sdk-manager'

    if (name.startsWith('lib')) {
        apply plugin: 'com.android.library'

        afterEvaluate {
            apply from: 'https://raw.github.com/shamanland/gradle-mvn-push/master/gradle-mvn-push.gradle'
        }
    } else {
        apply plugin: 'com.android.application'
    }

    android {
        compileSdkVersion COMPILE_SDK_VERSION as int
        buildToolsVersion BUILD_TOOLS_VERSION

        defaultConfig {
            minSdkVersion MIN_SDK_VERSION as int
            targetSdkVersion TARGET_SDK_VERSION as int
            versionCode 1
            versionName '1.0'

            // workaround for https://code.google.com/p/android/issues/detail?id=52962
            buildConfigField 'boolean', 'SNAPSHOT', VERSION_NAME.contains('SNAPSHOT').toString()
        }

        lintOptions {
            abortOnError false
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }
}
